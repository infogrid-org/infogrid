pkgname=$(basename $(pwd))
pkgver=2.9.9
pkgrel=1
pkgdesc='InfoGrid GraphDB'
arch=('any')
url="http://infogrid.org/"
license=('AGPL')
makedepends=(
    'maven'
    'jdk8-openjdk')
depends=(
    'java-runtime'
    'diet4j'
    'infogrid-stores')

prepare() {
    # Set pom.xml versions correctly; depends on XML-comment-based markup in pom.xml files
    find ${startdir} -name pom.xml -exec perl -pi -e "s/(?<=\<\!-- PKGVER -->)(\d+(\.\d+)+)(?=\<\!-- \/PKGVER -->)/${pkgver}/g" {} \;
}

build() {
    cd ${startdir}
    mvn package install ${MVN_OPTS}
}

package() {
    installOne 'org.infogrid.codegen'
    installOne 'org.infogrid.kernel'
    installOne 'org.infogrid.kernel.active'
    installOne 'org.infogrid.meshbase.security.aclbased'
    installOne 'org.infogrid.meshbase.store'
    installOne 'org.infogrid.model.Test'
}

installOne() {
    local name=$1
    install -m644 -D ${startdir}/${name}/target/${name}-${pkgver}.jar ${pkgdir}/usr/lib/java/org/infogrid/${name}/${pkgver}/${name}-${pkgver}.jar
}
